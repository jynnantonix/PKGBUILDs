# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=tabbed-git
_pkgname=tabbed
pkgver=20130218
pkgrel=1
pkgdesc="Simple generic tabbed fronted to xembed aware applications"
arch=('i686' 'x86_64')
url="http://tools.suckless.org/tabbed/"
license=('MIT/X')
depends=('libx11')
makedepends=('git')
provides=('tabbed')
conflicts=('tabbed')
source=( config.h )
md5sums=('fd33811c62c6e103657a8fbb78c8b78e')

_gitroot="http://git.suckless.org/tabbed"
_gitname="tabbed"

build() {
    cd "$srcdir"
    msg "Connecting to GIT server..."

    if [ -d $_gitname ] ; then
	cd $_gitname && git pull origin
	msg "The local files are updated."
    else
	git clone $_gitroot $_gitname
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    [ -d "$srcdir/$_gitname-build" ] && rm -rf "$srcdir/$_gitname-build"
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
    cd "$srcdir/$_gitname-build"

    cp "${srcdir}/config.h" config.h
    sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
    sed -i 's/^CFLAGS =/CFLAGS +=/g' config.mk
    sed -i 's/^LDFLAGS =/LDFLAGS +=/g' config.mk
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "$srcdir/$_gitname-build"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
    install -m644 -D README "$pkgdir/usr/share/doc/$_pkgname/README"
}
