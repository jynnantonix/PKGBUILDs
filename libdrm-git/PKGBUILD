# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Arch libdrm Contributors:
#	Jan de Groot
#	Andreas Radke

pkgname=libdrm-git
pkgver=20130107
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libpciaccess')
provides=('libdrm=2.4.40')
makedepends=('git' 'cairo' 'udev' 'valgrind')
options=('!libtool')
url="http://dri.freedesktop.org/"
conflicts=('libdrm')

_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='drm'

build() {

    cd "${srcdir}"
    msg "Connecting to GIT server...."

    if [[ -d ${_gitname} ]]; then
	cd "${_gitname}" && git pull origin
	msg "The local files are updated."
    else
	git clone "${_gitroot}" "${_gitname}"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting build..."

    rm -rf "${srcdir}/${_gitname}-build"
    git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
    cd "${srcdir}/${_gitname}-build"

    sed -i 's/PKG_CHECK_MODULES(PTHREADSTUBS, pthread-stubs)//' configure.ac

    ./autogen.sh
    ./configure --prefix=/usr \
	--enable-udev \
	--enable-intel \
	--enable-radeon \
	--enable-nouveau \
	--enable-vmwgfx
    make V=1
}

check() {
    cd "${srcdir}/${_gitname}-build"
    make -k check
}

package() {
    cd "${srcdir}/${_gitname}-build"
    make DESTDIR="${pkgdir}" install
}
