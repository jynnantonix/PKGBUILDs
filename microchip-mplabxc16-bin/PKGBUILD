# Maintainer: joat <joat at lavabit dot com>
# Submitter: BxS <bxsbxs at gmail dot com>

pkgname=microchip-mplabxc16-bin
pkgver=1.11
pkgrel=4
pkgdesc="C compiler for PIC24 MCUs and dsPIC DSCs"
arch=(i686 x86_64)
url=http://www.microchip.com/xc16
license=(custom)
depends=(xclm-dirs)
[[ $CARCH = x86_64 ]] && depends+=(lib32-gcc-libs)
[[ $CARCH = x86_64 ]] && makedepends=(lib32-fakeroot)
provides=(mplabxc16)
conflicts=(mplabxc16)
options=(!strip docs libtool emptydirs !zipman)
install=$pkgname.install
instdir=/opt/microchip/xc16/v$pkgver
installer=xc16-v$pkgver-linux-installer.run
source=(http://ww1.microchip.com/downloads/en/DeviceDoc/xc16-v$pkgver-linux-installer.run)
md5sums=(d642837097b252811de5f07f9b533abf)


package() {
  echo -e "Creating the Package\n  Please wait..."

  mkdir -p "$pkgdir$instdir"
  chmod 0755 "$srcdir/$installer"
  echo -e "\n\n\n\n\n\n\n\n\n\n\n\n\ny\n\n\n\ny\n\n$pkgdir$instdir\n\ni\ni\n" > /tmp/inst_input

  ORIGINAL_LD=$LD_LIBRARY_PATH
  [[ $CARCH = x86_64 ]] && LD_LIBRARY_PATH=/usr/lib32/libfakeroot:\$LD_LIBRARY_PATH
  "$srcdir/$installer" --mode text </tmp/inst_input
  LD_LIBRARY_PATH=$ORIGINAL_LD

  rm -r /tmp/inst_input "$pkgdir$instdir"/{Uninstall*,etc,rollbackBackupDirectory}
  rm -r "$pkgdir$instdir"/bin/{roam.lic,xclm}

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$instdir/MPLAB_XC16_Compiler_License.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
