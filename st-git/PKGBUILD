# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
pkgver=0.4.1.24.g8f47c4a
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://suckless.org/st/'
license=('MIT')
depends=('libxext' 'libxft')
makedepends=('git' 'ncurses')
arch=('i686' 'x86_64' 'armv7h')
provides=('st')
conflicts=('st')
replaces=('st-hg')
changelog=CHANGELOG
source=( "git://git.suckless.org/st"
    0001-Use-control-as-mod-key.patch
    0002-Use-2-spaces-for-tabs.patch
    0003-Use-terminus-font.patch
    0004-Use-zsh.patch
    0005-Emulate-emacs-yank-with-ModMask-y.patch
    0006-Use-zenburn-colors.patch )
md5sums=('SKIP'
    '5dd5a2745952f5b9c989f264cd4bf76b'
    'da6859538e188c8c4c91b9418f46aeca'
    '69ccb0200f858a03486b9ba107b699d0'
    '292fcdf9bee584d0284889a272ec4bca'
    '24f38452e05308ecaf1e9763fe389009'
    '1c424777d3c83720f4c94e3e7df967cd')

pkgver() {
    cd st
    git describe --tags | sed 's/-/./g'
}

prepare() {
    cd st
    for i in $(find ../ -name '*.patch' | sort); do
	msg2 "Applying $i..."
	git apply $i
    done
}

build() {
    cd st
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd st
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
    install -m644 -D README "$pkgdir/usr/share/doc/$_pkgname/README"
}
