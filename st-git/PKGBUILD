# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
pkgver=0.4.1.24.g8f47c4a
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://suckless.org/st/'
license=('MIT')
depends=('libxext' 'libxft')
makedepends=('git' 'ncurses')
arch=('i686' 'x86_64' 'armv7h')
provides=('st')
conflicts=('st')
replaces=('st-hg')
changelog=CHANGELOG
source=( "git://git.suckless.org/st"
    0001-Use-control-as-mod-key.patch
    0002-Use-2-spaces-for-tabs.patch
    0003-Use-terminus-font.patch
    0004-Use-zsh.patch
    0005-Use-zenburn-colors.patch
    0006-Use-MODKEY-y-to-paste.patch
    0007-Tag-README-as-a-markdown-file.patch )
md5sums=('SKIP'
    '7efe3cdeb537c609940088225c4c2020'
    '49c8234a9281be80890238dc5908d45c'
    'e13cb28c48c4313271192bb612de91bd'
    '09d88bf8e7ddd5ffaf7b66704d8fab0f'
    'fcdbf8c4d292dd8d58ea894c2f149660'
    'f07ec97fd716faf43aac7abd56725309'
    '34b19d5f48579b6644930b25a72e68cc')

pkgver() {
    cd st
    git describe --tags | sed 's/-/./g'
}

prepare() {
    cd st
    for i in $(find ../ -name '*.patch' | sort); do
	msg2 "Applying $i..."
	git apply $i
    done
}

build() {
    cd st
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd st
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
    install -m644 -D README.md "$pkgdir/usr/share/doc/$_pkgname/README"
}
