# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Popsch <popsch@gmx.net>
# Thanks to the maintainers and contributors of the mu binary package

pkgname=mu-git
pkgver=0.9.9.5.110.g08748f2
pkgrel=1
pkgdesc="mu and mu4e from git"
arch=('i686' 'x86_64')
url="http://www.djcbsoftware.nl/code/mu"
depends=('xapian-core' 'gmime' 'guile>=2.0')
makedepends=('git')
optdepends=('emacs: required for mu4e')
license=('GPL')
provides=('mu')
conflicts=('mu')
changelog=CHANGELOG
install=mu-git.install
source=("git://github.com/djcb/mu.git"
    0001-Drop-misplaced-braces.patch
    0002-fix-file-size-in-test-mu-guile.scm-changed-by-e97ec2.patch)
md5sums=('SKIP'
    '948578c42c79ef447b974ff220b3a4e5'
    'bf10877fcdf0b4d3883bb86ea6a7f045')

pkgver() {
    cd mu
    git describe --tags | sed 's/-/./g;s/v//g'
}

prepare() {
    cd mu

    for p in $(find ../ -name '*.patch' | sort); do
	msg2 "Applying $p..."
	git apply "$p"
    done
}

build() {
    cd mu

    autoreconf -i
    ./configure --prefix=/usr

    make
}

check() {
    make -C mu check
}

package() {
    make -C mu DESTDIR="$pkgdir" install
}
