# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>

pkgname=wanderlust-git
pkgver=e53d797
pkgrel=1
pkgdesc="Mail/News reader supporting IMAP4rev1 for emacs"
url="https://github.com/ikazuhiro/wanderlust"
arch=('any')
license=('GPL')
depends=('emacs' 'emacs-apel' 'flim' 'semi')
makedepends=('bbdb' 'git')
optdepends=('bbdb: contact management utility')
provides=('wanderlust')
conflicts=('wanderlust')
install=wanderlust.install
changelog=CHANGELOG
source=("git://github.com/ikazuhiro/wanderlust")
md5sums=('SKIP')

_pixmapdir=/usr/share/emacs/$(emacs -batch -eval "(princ (format \"%d.%d\" emacs-major-version emacs-minor-version))")/etc/wl/icons/
_lispdir=/usr/share/emacs/site-lisp
_infodir=/usr/share/info

pkgver() {
  cd 'wanderlust'
	git rev-parse --short HEAD
}

build() {
	make -C wanderlust all info \
		PIXMAPDIR="$_pixmapdir" \
		INFODIR=$_infodir \
		LISPDIR=$_lispdir
}

package() {
	install -dm0755 "$pkgdir$_infodir"
	make -C wanderlust install install-info \
		LISPDIR="$pkgdir$_lispdir" \
		PIXMAPDIR="$pkgdir$_pixmapdir" \
		INFODIR="$pkgdir$_infodir"

	install -m644 wanderlust/utils/ssl.el "$pkgdir/usr/share/emacs/site-lisp/ssl.el"
}
