# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Brian Schubert <bewschubert@gmail.com>

_build_doc=ON
_build_apps=ON
pkgname=openmesh
pkgver=2.3
pkgrel=1
pkgdesc="A generic and efficient data structure for representing and manipulating polygonal meshes"
arch=('i686' 'x86_64')
url="http://www.openmesh.org"
license=('custom')
depends=('mesa')
optdepends=('qt: for using included applications' )    
install=openmesh.install
source=("${pkgname}-${pkgver}.tar.bz2::http://www.openmesh.org/fileadmin/openmesh-files/${pkgver}/OpenMesh-${pkgver}.tar.bz2"
    doc-install.patch)
md5sums=('6b7102306d1dcdf5376486659010897c'
         'b454d39c3700ccd515fa47be69ffa5b2')

if [[ "${_build_doc}" == "ON" && "${_build_apps}" == "ON" ]]; then
    makedepends=('cmake' 'qt' 'graphviz' 'doxygen')
elif [[ "${_build_doc}" == "ON" ]]; then
    makedepends=('cmake' 'graphviz' 'doxygen')
elif [[ "${_build_apps}" == "ON" ]]; then
    makedepends=('cmake' 'qt')
else
    makedepends=('cmake')
fi

build() {
    if [[ "${_build_doc}" == "ON" ]]; then
	patch -Np0 -i doc-install.patch
    fi

    mkdir -p "${srcdir}"/build
    cd "${srcdir}"/build
    
    cmake ../OpenMesh-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX="${pkgdir}"/usr \
        -DBUILD_APPS=${_build_apps}
    make

    if [[ "${_build_doc}" == "ON" ]]; then
	make doc
    fi
}

package() {
    cd "${srcdir}"/build
    make install
    
    # install licenses
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/COPYING \
        "${pkgdir}"/usr/share/licenses/openmesh/COPYING
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/COPYING.EXCEPTIONS \
        "${pkgdir}"/usr/share/licenses/openmesh/COPYING.EXCEPTIONS
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/COPYING.LESSER \
        "${pkgdir}"/usr/share/licenses/openmesh/COPYING.LESSER
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/LICENSEHEADER \
        "${pkgdir}"/usr/share/licenses/openmesh/LICENSEHEADER

    # add openmesh library path to ld.so.conf
    install -d -m755 "${pkgdir}"/etc/ld.so.conf.d/
    echo "/usr/lib/OpenMesh" > "${pkgdir}"/etc/ld.so.conf.d/openmesh.conf
}
