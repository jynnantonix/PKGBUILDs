# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Brian Schubert <bewschubert@gmail.com>

pkgname=openmesh
pkgver=2.3
pkgrel=1
pkgdesc="A generic and efficient data structure for representing and manipulating polygonal meshes"
arch=('i686' 'x86_64')
url="http://www.openmesh.org"
license=('custom')
depends=('mesa')
optdepends=('qt: for using included applications' )    
install=openmesh.install
source=("${pkgname}-${pkgver}.tar.bz2::http://www.openmesh.org/fileadmin/openmesh-files/${pkgver}/OpenMesh-${pkgver}.tar.bz2"
    doc-install.patch)
md5sums=('6b7102306d1dcdf5376486659010897c'
         'b454d39c3700ccd515fa47be69ffa5b2')

# remove graphviz and doxygen if you don't plan on building documentation. 
# remove qt if you don't plan on building apps
makedepends=('cmake' 'qt' 'graphviz' 'doxygen')

build() {
    # apply patch so the documentation will be installed in $pkgdir
    # comment out this line if you don't want documentation
    patch -Np0 -i doc-install.patch

    mkdir -p "${srcdir}"/build
    cd "${srcdir}"/build
    
    # set -DBUILD_APPS=OFF if you don't want to build the apps
    cmake ../OpenMesh-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_APPS=ON
    make
}

package() {
    cd "${srcdir}"/build
    make DESTDIR="${pkgdir}" install
    
    # install licenses
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/COPYING \
        "${pkgdir}"/usr/share/licenses/openmesh/COPYING
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/COPYING.EXCEPTIONS \
        "${pkgdir}"/usr/share/licenses/openmesh/COPYING.EXCEPTIONS
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/COPYING.LESSER \
        "${pkgdir}"/usr/share/licenses/openmesh/COPYING.LESSER
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/LICENSEHEADER \
        "${pkgdir}"/usr/share/licenses/openmesh/LICENSEHEADER

    # add openmesh library path to ld.so.conf
    install -d -m755 "${pkgdir}"/etc/ld.so.conf.d/
    echo "/usr/lib/OpenMesh" > "${pkgdir}"/etc/ld.so.conf.d/openmesh.conf
}
