# Maintainer: Chirantan Ekbote <ekbotec at chromium.org>
# Contributor: Estevao Valadao <estevao@archlinux-br.org>
# Contributor: Sven Goericke <sgoericke@member.fsf.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>
# Contributor: Nagy Gabor <ngaba@@bibl..u-szeged..hu>

pkgname=cairo-cleartype
pkgver=1.12.16
pkgrel=1
pkgdesc="Cairo vector graphics library with ClearType patch from Gentoo"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0'
    'glib2' 'mesa' 'libgl' 'sh' 'lzo2')
makedepends=('librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc' 'valgrind'
    # for the test suite:
    'ttf-dejavu' 'gsfonts' 'xorg-server-xvfb' ) # 'libdrm')
#optdepends=('xcb-util: for XCB backend') # really needed?
options=('!libtool')
conflicts=('cairo' 'cairo-xcb')
provides=("cairo=${pkgver}" "cairo-xcb")
changelog=ChangeLog
source=(http://cairographics.org/releases/cairo-$pkgver.tar.xz
    cairo-1.12.16-buggy-gradients.patch
    cairo-1.12.16-cleartype.patch)
md5sums=('a1304edcdc99282f478b995ee5f8f854'
    '9f0ee98356072d8a80d71d15d8896b90'
    '7538ff65ad91cf3fe49a266553081d14')

prepare() {
    cd cairo-$pkgver

    patch -Np1 -i "${srcdir}/cairo-1.12.16-buggy-gradients.patch"
    patch -Np1 -i "${srcdir}/cairo-1.12.16-cleartype.patch"
}

build() {
    cd cairo-$pkgver

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --enable-tee \
        --enable-gl \
        --enable-egl \
        --enable-svg \
        --enable-ps \
        --enable-pdf \
        --enable-gobject #\
        # --disable-xlib-xcb \
        # --enable-egl \ # only if you have mesa-libgl
        # --enable-test-surfaces \ # takes ages
        # --enable-drm # breaks build

    make
}

# check() {
#     cd cairo-$pkgver

#     make -k test || /bin/true
#     # results:
#     # 1.12.8-1	# 162 Passed, 328 Failed [8 crashed, 10 expected], 26 Skipped
#     # 1.12.12-2:	# 29 Passed, 464 Failed [460 crashed, 2 expected], 26 Skipped
#     # 1.12.16-1:	144 Passed, 364 Failed [6 crashed, 12 expected], 27 Skipped
# }

package() {
    cd cairo-$pkgver
    make DESTDIR="$pkgdir" install
}
