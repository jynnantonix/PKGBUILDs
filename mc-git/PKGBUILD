# Maintainer: Chirantan Ekbote <chirantan.ekbote@gmail.com>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Tsekhovoy Eugene aka Krash <8552246@gmail.com>

pkgname=mc-git
pkgver=4.8.8.11.gf1376d4
pkgrel=1
pkgdesc='GNU Midnight Commander is an s-lang based file manager'
url='https://midnight-commander.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'slang' 'perl')
makedepends=('git')
conflicts=('mc' 'mc-cvs' 'mc-fork-svn' 'mc-mp' 'mc-slavz' 'mc-suse')
provides=('mc')
changelog=CHANGELOG
source=('git://github.com/MidnightCommander/mc')
md5sums=('SKIP')

pkgver() {
    cd mc
    git describe | sed 's/-/./g'
}

build() {
    cd mc
    # http://git.yoctoproject.org/cgit.cgi/poky-contrib/commit/?h=dcui/mc&id=338da10abcd744ab75dfb184f8f81370727e5653
    sed -i 's/pkglibexec_SCRIPTS = $(SCRIPTS_OUT)/pkgdata_SCRIPTS = $(SCRIPTS_OUT)/g' contrib/Makefile.am
    ./autogen.sh
    export MCREVISION=$(git describe --tags)
    ./configure                     \
	--prefix=/usr               \
	--sysconfdir=/etc           \
	--libexecdir=/usr/lib       \
	--enable-background         \
	--enable-network            \
	--enable-netcode            \
	--enable-charset            \
	--enable-nls                \
	--with-vfs                  \
	--with-edit                 \
	--with-screen=slang         \
	--without-x                 \
	--without-samba             \
	--without-gpm-mouse         \
	--without-gnome             \
	--without-debug             \
	--without-included-gettext  \
	--disable-dependency-tracking
    make
}

package() {
    cd mc
    make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 ft=sh et:
