# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Thomas Dziedzic <gostrc@@@gmail>
# Contributor: Antti "Tera" Oja <antti.bofh@@@gmail.com>
# Contributor: JiÅ™Ã­ ZÃ¡revÃºcky <zarevucky.jiri@@@gmail.com>

pkgname=dri2proto-git
pkgver=2.8.2.gead89ad
pkgrel=1
pkgdesc='X11 DRI extension wire protocol.'
url='http://xorg.freedesktop.org/'
arch=('any')
license=('custom')
makedepends=('xorg-util-macros' 'git')
provides=('dri2proto')
conflicts=('dri2proto')
changelog=CHANGELOG
source=('git://git.freedesktop.org/git/xorg/proto/dri2proto')
md5sums=('SKIP')

pkgver() {
	cd 'dri2proto'
	git describe --always | sed 's|dri2proto.||g;s|-|.|g'
}

build() {
	cd dri2proto

	./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd dri2proto

	make DESTDIR="$pkgdir" install
	install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
