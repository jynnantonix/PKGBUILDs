pkgname=dwm-git
pkgver=6.0.32.g7edc596
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
makedepends=('git')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
changelog=CHANGELOG
source=( "git://git.suckless.org/dwm"
    0001-Change-font-to-size-10-and-change-modkey-to-mod4.patch
    0002-Append-compiler-flags-instead-of-overwriting-them.patch
    0003-Don-t-show-the-status-bar-on-startup.patch
    0004-Make-border-color-black-add-volume-control-options.patch
    dwm.desktop)
md5sums=('SKIP'
    'a945a06c290d38df7941277b89fe147c'
    '6dc8970048f626af3316040acda036ac'
    '7763100e9624b3d00e292c6972f4b4e2'
    'be0b8fe8c8b78de359f6c4bca346f45c'
    '939f403a71b6e85261d09fc3412269ee')

pkgver() {
    cd dwm
    git describe --tags | sed 's|-|.|g'
}

prepare() {
    cd dwm

    for f in $(find ../ -name '*.patch' | sort); do
	msg2 "Applying $f"
	git apply "$f"
    done
}

build() {
    cd dwm
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd dwm
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
    install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
