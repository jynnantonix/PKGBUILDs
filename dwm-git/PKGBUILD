pkgname=dwm-git
pkgver=6.0.35.gb800a1d
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
makedepends=('git')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
changelog=CHANGELOG
source=( "git://git.suckless.org/dwm"
    0001-Change-font-to-size-10-and-change-modkey-to-mod4.patch
    0002-Append-compiler-flags-instead-of-overwriting-them.patch
    0003-Don-t-show-the-status-bar-on-startup.patch
    0004-Make-border-color-black-add-volume-control-options.patch
    0005-Only-change-focus-on-mouse-click.patch
    0006-Add-windows-above-selected-window.patch
    0007-Revert-Only-change-focus-on-mouse-click.patch
    dwm.desktop)
md5sums=('SKIP'
         'ad726d5f5e25866d65f2a8f732defb96'
         '44c2e990c8cb886441c73c9340a28997'
         '01e9e047735edf8e02507384233a867b'
         '8fe2c3c9e8a6f928c5e0de7b134e0c3d'
         '207a8bf7acb2e8142e5dd5472f24a39b'
         '412000f7ac84a422edd03e66ee450981'
         'b9e2d3de53fab7668826089bd69a7d7d'
         '939f403a71b6e85261d09fc3412269ee')

pkgver() {
    cd dwm
    git describe --tags | sed 's|-|.|g'
}

prepare() {
    cd dwm

    for f in $(find ../ -name '*.patch' | sort); do
        msg2 "Applying $f"
        git apply "$f"
    done
}

build() {
    cd dwm
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd dwm
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
    install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
