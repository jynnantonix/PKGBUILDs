# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: juergen <juergen@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>

pkgname=scheme48
pkgver=1.9
pkgrel=1
pkgdesc="Scheme 48 is an implementation of Scheme"
url="http://www.s48.org/index.html"
arch=('i686' 'x86_64')
depends=('glibc')
license=('BSD')
source=("http://www.s48.org/$pkgver/scheme48-$pkgver.tgz"
    "scheme48-1.9-makefile.patch")
md5sums=('b4c20057f92191d05a61fac1372878ad'
         '7c1f8d65c1e9f3d086655d00f3ad3bb2')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    # This package won't build if --as-needed is set in LDFLAGS.  So the
    # following three lines strip out --as-needed.
    # For more, see this thread on the Arch forums:
    # http://bbs.archlinux.org/viewtopic.php?pid=668686
    # Also see http://bugs.gentoo.org/157188
    export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
    export LDFLAGS="${LDFLAGS//,--as-needed}"
    export LDFLAGS="${LDFLAGS//--as-needed}"
    patch -p1 -i "$srcdir/scheme48-1.9-makefile.patch"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install-no-doc
    install -d "$pkgdir/usr/share/licenses/$pkgname"
    mv "$pkgdir/usr/share/doc/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
